// http://www.albertgao.xyz/2018/01/18/how-to-publish-artifact-to-maven-central-via-gradle/
//   - $ ./gradlew clean build test publishToMavenLocal
//   - $ ./gradlew clean build test uploadArchives
//   - https://oss.sonatype.org/#stagingRepositories
//   - $ ./gradlew closeAndReleaseRepository


buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
	}
}

plugins {
	id 'java-library'
	id 'maven-publish'
	id 'io.codearte.nexus-staging' version '0.21.1'
	id "org.sonarqube" version "2.8"
}

apply plugin: 'com.bmuschko.nexus'

archivesBaseName = 'api-camp-data'
group = 'io.github.de314'
version = '1.0.0'
sourceCompatibility = '1.8'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	compile 'org.rocksdb:rocksdbjni:5.5.1'

	compile group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: '1.11.671'

	compile 'com.fasterxml.jackson.core:jackson-databind:2.9.10'
//	compile 'com.github.java-json-tools:json-schema-validator:2.2.11'

	compile group: 'com.google.guava', name: 'guava', version: '23.0'

	implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.28'
	implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

	compileOnly 'org.projectlombok:lombok:1.18.10'
	annotationProcessor 'org.projectlombok:lombok:1.18.10'


	testCompile 'org.projectlombok:lombok:1.18.10'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.10'
	testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.5.2'
	testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.5.2'
	testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
}

test {
	useJUnitPlatform()
}

sonarqube {
	properties {
		property "sonar.projectKey", "de314_api-camp-data-library"
	}
}

modifyPom {
	project {
		name 'api-camp-data'
		description 'Embedded datastore utilities for REST BFF"s.'
		url 'https://github.com/de314/data-embedded-library'
		inceptionYear '2019'

		scm {
			url 'https://github.com/de314/data-embedded-library'
			connection 'scm:https://github.com/de314/data-embedded-library.git'
			developerConnection 'scm:git@github.com:de314/data-embedded-library.git'
		}

		licenses {
			license {
				name 'The Apache Software License, Version 2.0'
				url 'https://github.com/de314/data-embedded-library/blob/master/LICENSE'
				distribution 'repo'
			}
		}

		developers {
			developer {
				id 'de314'
				name 'David Esposito'
				email 'david@de314.com'
			}
		}
	}
}

extraArchive {
	sources = true
	tests = true
	javadoc = true
}

nexus {
	sign = true
	repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
	snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}

nexusStaging {
	packageGroup = "io.github.de314"
//	stagingProfileId = "yourStagingProfileId" //when not defined will be got from server using "packageGroup"
}